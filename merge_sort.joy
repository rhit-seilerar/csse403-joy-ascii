"joy/inilib.joy" include.
"joy/numlib.joy" include.
"joy/agglib.joy" include.

DEFINE 

to_sort == [8 1 3 2 4 7 5 6];
split_helper == dup size rolldown dup size swapd 
    [>=] 
    [pop pop swap] 
    [pop pop uncons rotate cons split_helper] 
    ifte;
reverse == [] [swons] fold;
merge_helper == rollup 
    [
        [[null] pop swap [swons] fold]
        [[swap null] swap pop swap [swons] fold]
        [
            unswons rolldown uncons rollup
            [>=]
            [rolldownd rollup cons rolldownd rollup cons]
            [swapd swons rolldownd rollup cons]
            ifte
            merge_helper]] 
    cond;
my_split == [] split_helper;
merge == [] merge_helper reverse;
merge_sort == [size 1 <=] [] [my_split] [merge] binrec.

to_sort merge_sort.
["Hi" "can" "we" "sort" "these" "strings" "?"] merge_sort.

